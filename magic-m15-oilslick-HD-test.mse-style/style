mse version: 2.0.0
game: magic
short name: HD油闪
full name: HD oilslick frame
icon: card-sample.png
installer group: magic/m15 style/normal cards
position hint: 001

version: 2021-02-16
depends on:
	package: magic.mse-game
	version: 2014-06-25
depends on:
	package: magic-default-image.mse-include
	version: 2007-09-23
depends on:
	package: magic-watermarks.mse-include
	version: 2016-03-13
depends on:
	package: magic-identity-new.mse-include
	version: 2012-01-22
depends on:
	package: magic-mana-oilslick.mse-symbol-font
	version: 2007-09-23
depends on:
	package: magic-mainframe-extras.mse-include
	version: 2007-09-23
depends on: 
	package: magic-rarity.mse-include
	version: 2013-08-21
depends on: 
	package: magic-all-style.mse-include
	version: 2024-06-30

card width: 750
card height: 1050
card dpi: 600

############################################################## Extra scripts
init script:
	# Load scripts for image box
	include file: /magic-default-image.mse-include/scripts
	include file: /magic-all-style.mse-include/script
	
	# Should hybrids have a grey name?
	mask_hybrid_with_land := { styling.grey_hybrid_name }
	
	#Should multicolor lands with basic land types have a colored name?
	mask_multi_land_with_color := { styling.colored_multicolor_land_name }
	
	template_prefix := [ card:"normal/" crown:"crown/" pt: "pt/" stamp: "stamp/" identity: "/magic-identity-new.mse-include/" ]
	template_suffix_normal := [ card: "card.png" crown:"crown.png" pt: "pt.png" stamp: "stamp.png" identity: "identity.png" ]
	
	template      := { template_prefix[type] + input + crown_follow(type) + template_suffix_normal[type]}
	land_template := { template_prefix[type] + (if input == "a" then "c" else input) + crown_follow(type) + template_suffix_normal[type] }
	
	
	# Use land templates for previews because they show more contrast
	hybrid_previews := "land,hybrid"
	
	crown_background := { color_background(type:"crown",     base_hybrid:card_hybrid) }

	crown_follow := {
		if input == "crown" then if is_legend() then "l" else "" else ""
	}

	# Use the normal tap symbol
	mana_t := {
		if      styling.tap_symbol == "old"        then "old"
		else if styling.tap_symbol == "diagonal T" then "older"
		else                                            "new"
	}
	
	# Use guild mana symbols?
	guild_mana := { styling.use_guild_mana_symbols }
	
	# Is the card a promo card?
	is_promo := { styling.promo }
	is_legend := {set.auto_legends and (contains(card.super_type, match:"Legendary") or contains(card.super_type, match:"传奇") or contains(card.type, match:"Mewk")) }
	
	mana_sort := {
		if styling.unsorted_casting_cost then
			input
		else
			sort_text@(order: "XYZI[0123456789]HSCA(WUBRG)")()
	}
	ancestral_mana := { styling.ancestral_mana_symbols }
	has_identity := { styling.color_indicator_dot }
	is_unsorted := {styling.remove_from_autocount}

	use_main_rarity := { contains(set.mainframe_rarity_name, match: ".png")}
	un_png := replace@(match:".png", replace: "")
	
	# d20
	is_d20 := {styling.d20_dividers != "off"}
	is_all_break := {contains(styling.d20_dividers, match:"all breaks")}
	d20string := "^(<b>)?(((</sym>)?[-0-9 ,+>\<](<sym>)?)+|[Ee]lse|[Oo]ther) ?(</b>)?[|]"
	d20_array := 
		filter_text@(match:"(\n|"+d20string+")")
		+split_text@(match:d20string)
	number_of_rolls := {if is_all_break() then 1+length(filter_text(card.rule_text, match:"\n"))-rolls_start(card.text) else length(d20_array(input))-1}
	rolls_start := {
		if styling.d20_dividers == "all breaks +3" then 4
		else if styling.d20_dividers == "all breaks +2" then 3
		else if styling.d20_dividers == "all breaks +1" then 2
		else if styling.d20_dividers == "all breaks" then 0
		else if is_d20() then length(d20_array(input).0)
		else 0
	}
	d20_filter := replace@(match:"^([Ee]lse ?|[Oo]ther ?|((</sym>)?[-0-9 ,+>\<](<sym>)?)+)[|]", replace:"<b>\\1</b>|")
	#"
	top_of_para := {
		input := input + no_roll(card.text)
		if card_style.text.layout.paragraphs[input].top or else 0 > 0 then
			card_style.text.top + card_style.text.layout.paragraphs[input].top - 0.5*(card_style.text.layout.paragraphs[input].top-card_style.text.layout.paragraphs[input-1].bottom)
		else 0
	}
	height_of_para := {
		input := input + no_roll(card.text)
		if card_style.text.layout.paragraphs[input].bottom or else 0 > 0 then
			card_style.text.layout.paragraphs[input].bottom + 1.3*(card_style.text.layout.paragraphs[input].top-card_style.text.layout.paragraphs[input-1].bottom) - card_style.text.layout.paragraphs[input].top
		else 0
	}
	no_roll := {if styling.d20_dividers == "all breaks" then 1 else if rolls_start(input) == 0 then 1 else 0}
	text_filter := text_filter + d20_filter + {input}
	overlay1 := {if is_d20() then "d20slice.png" else slice_crop(background_slide(), height: lv_2_height(), width:630, distance:(152-lv_2_coordinate()))}
	overlay2 := {if is_d20() then "d20slice.png" else slice_crop(background_slide_2(), height: lv_3_height(), width:630, distance:lv_3_height())}
	overlay3 := {if is_d20() then "d20slice.png" else slice_crop(background_slide_3(), height: lv_4_height(), width:630, distance:lv_4_height())}
	
############################################################## Set info fields
set info style:
	symbol:
		variation:
			name: invertedcommon
			border radius: 0.10
			fill type: solid
			fill color:   rgb(255,255,255)
			border color: rgb(0,0,0)
############################################################## Extra style options
### 通用1 ###
include file: /magic-all-style.mse-include/style1

styling field:
	type: choice
	name: d20 dividers
	choice: d20 mode
	choice: off
	choice: all breaks
	choice: all breaks +1
	choice: all breaks +2
	choice: all breaks +3
	description: 正常地d20分隔，关闭，每一段隔断，在除第一段以外每一段隔断，等等。

styling field:
	type: boolean
	name: use holofoil stamps
	description: Change to no to disable rare holofoil stamps
styling field:
	type: boolean
	name: is foil
	description: 是否为闪卡？
	initial: no

### 通用2 ###
include file: /magic-all-style.mse-include/style2

styling style:
	use guild mana symbols:
		choice images:
			yes: /magic-mana-oilslick.mse-symbol-font/mana_guild_rg.png
			no:  /magic-mana-oilslick.mse-symbol-font/mana_rg.png
	tap symbol:
		render style: both
		choice images:
			modern:     /magic-mana-oilslick.mse-symbol-font/mana_t.png
			old:        /magic-mana-oilslick.mse-symbol-font/mana_t_old.png
			diagonal T: /magic-mana-oilslick.mse-symbol-font/mana_t_older.png
	center text:
		render style: both
		choice images:
			always:          /magic.mse-game/icons/center-all-text.png
			short text only: /magic.mse-game/icons/center-short-text.png
			never:           /magic.mse-game/icons/center-no-text.png
	inverted common symbol:
		choice images:
			no:  { symbol_variation(symbol: set.symbol, variation: "common") }
			yes: { symbol_variation(symbol: set.symbol, variation: "invertedcommon") }

############################################################## Card fields
card style:
	############################# Background stuff
	card color:
		left:	0
		top:	0
		width:	750
		height:	1050
		z index: 0
		render style: image
		popup style: in place
		image: { card_background() }
		
		mask: { if card.pt != "" then "normal/frame_mask_pt.png" else "normal/frame_mask.png" }
	############################# Name line
	name:
		left: 64
		top: {4 + if set.card_language != "简体中文" then 59 else 57 }
		right:  { 682 - max(30, card_style.casting_cost.content_width) }
		height: {46 - 0.5*shrink_name()}
		alignment: { "bottom shrink-overflow" }
		padding bottom: 0
		z index: 5
		font:
			name: { if set.card_language != "简体中文" then "Beleren Bold" else "华康魏碑 Std W7" }
			size: { if set.card_language != "简体中文" then 32 - shrink_name() else 34 - shrink_name()}
			color: white
	casting cost:
		right: 692
		top: {4 + 57}
		width: { max(60, card_style.casting_cost.content_width) + 10 }
		height: 46
		alignment: middle right
		font:
			name: MPlantin
			size: 30
		symbol font:
			name: magic-mana-oilslick
			size: 30
			alignment: middle right
		always symbol: true
		z index: 2
		padding top: 0
	############################# Image
	image:
		left: 0
		top: 0
		width: 750
		height:	{ if card.pt != "" then 996 else 978 }
		z index: -1
		default: {default_image(card.card_color)}
	############################# Card type
	indicator:
		left: 62
		top: { 604 }
		width: 34
		height: 34
		z index: 1
		render style: image
		visible: { has_identity() }
		image: { card_identity() }
	type:
		left: { if has_identity() then "102" else "62" }
		top: { shrink_type()/2 + 598 }
		width: { (if has_identity() then "580" else "610") - max(44,card_style.rarity.content_width) }
		height: { 40 - shrink_type() }
		alignment: top shrink-overflow
		z index: 2
		padding top: 2
		font:
			name: { if set.card_language != "简体中文" then "Beleren Bold" else "华康魏碑 Std W7" }
			size: { if set.card_language != "简体中文" then 26 - shrink_type() else 32 - shrink_type() }
			color: white
			separator color: red
	rarity:
		right: { 688 + rarity_shift() }
		top: { 598 }
		width: 88
		height: 44
		z index: 2
		render style: image
		alignment: middle right
		include file: /magic-rarity.mse-include/rarity
		choice images:
			# Images based on the set symbol
			basic land:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "c.png"
					else if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
					else symbol_variation(symbol: set.symbol, variation: "common")
			common:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "c.png"
					else if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
					else symbol_variation(symbol: set.symbol, variation: "common")
			uncommon:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "u.png"
					else symbol_variation(symbol: set.symbol, variation: "uncommon")
			rare:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "r.png"
					else symbol_variation(symbol: set.symbol, variation: "rare")
			mythic rare:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "m.png"
					else symbol_variation(symbol: set.symbol, variation: "mythic rare")
			special:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "s.png"
					else symbol_variation(symbol: set.symbol, variation: "special")
			masterpiece:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "mp.png"
					else symbol_variation(symbol: set.masterpiece_symbol, variation: "mythic rare")
	############################# Text box
	text:
		left: 58
		top: { 654-(-chop_top()) }
		width: 636
		bottom: {960-chop_bot()}
		font:
			name: { if set.card_language != "简体中文" then "MPlantin" else ch_font() }
			italic name: { if set.card_language != "简体中文" then "MPlantin-Italic" else ch_flavor_font() }
			size: {if styling.font_cap != "" then to_number(styling.font_cap) else 28 }
			scale down to: 6
			color: white
		symbol font:
			name: magic-mana-oilslick
			size: {if styling.font_cap != "" then to_number(styling.font_cap) else 28 }
			scale down to: 6
		alignment:
			script:
				if (styling.center_text == "short text only" and
				   not contains(match:"\n", card.rule_text) and
				   card.flavor_text == "<i-flavor></i-flavor>" and
				   card_style.text.content_lines <= 2) or
				   styling.center_text == "always"
				then "middle center"
				else "middle left"
				+ if styling.justify_text == "yes" then " justify-all" else if styling.justify_text == "force justify" then " force justify-all" else ""
		z index: 2
		padding left:   6
		padding right:  {4 + chop_right()}
		line height hard: 1.1
		line height line: { if chinese_new_font() then 1.6 else 1.5 }
		line height soft: { if chinese_new_font() then 1.0 else 0.9 }
		line height hard max: 1.2
		line height line max: { if chinese_new_font() then 1.7 else 1.6 }

		#padding right:  4
		#line height hard: {if is_modal(card.rule_text) then 0.9 else 1.1 }
		#line height line: 1.5
		#line height soft: 0.9
		#line height hard max: {if is_modal(card.rule_text) then 1.0 else 1.2 }
		#line height line max: 1.6
	watermark:
		left: 50
		top: { 676 -8}
		width: 650
		bottom: { 676 + 278 -8}
		z index: 1
		render style: image
		popup style: in place
		alignment: middle center
		include file: /magic-watermarks.mse-include/watermarks
	############################# PT
	pt:
		z index: 2
		left: 586
		top: 936
		width: 120
		height:	56
		alignment: center middle shrink-overflow
		font:
			name:  { "Beleren Bold" }
			size: {32 - (if styling.shrink_power_text != "" then to_number(styling.shrink_power_text) else 0)}
			color: white
			separator color: red
	############################# Card sorting / numbering
	set code:
		left: 48.5
		top: 998
		width: 80
		height: 20
		z index: 1
		alignment: middle left
		font:
			name: Magic Gotham Medium
			size: {13-shrink_setcode()}
			color: white

	language code:
		left: 106
		top: 998
		width: 10
		height: 20
		z index: 1
		alignment: middle left
		font:
			name: Magic Gotham Medium
			size: {13-shrink_setcode()}
			color: white

	############################# Copyright stuff
	illustrator:
		left: { 166 }
		top: 997
		width: 400
		height: 20
		z index: 1
		font:
			name: Beleren Small Caps Bold
			size: 14
			color: white
	copyright:
		right: 699
		top: { if card.pt != "" then 998 else 981 }
		width: 252
		height: 20
		z index: 2
		alignment: middle right shrink-overflow
		visible: {not set.automatic_copyright}
		font:
			name: mplantin
			size: 13
			color: white
############################################################## Extra card fields
extra card field:
	type: text
	name: dot
	script: if styling.is_foil then "★" else "•"
	save value: false
	editable: false
	show statistics: false
extra card field:
	type: text
	name: card code
	save value: false
	editable: false
	script: rarity_code()
extra card field:
	type: text
	name: card number
	save value: false
	script:
		if set.automatic_card_numbers and not styling.remove_from_autocount then
			forward_editor(prefix: card_number_m15(), field: card.card_code_text) 
		else
			forward_editor(prefix: "", field: card.custom_card_number)

extra card field:
	type: choice
	name: artist arrow
	editable: false
	save value: false
	choice: white
extra card field:
	type: choice
	name: pt box
	script: card.card_color
	editable: false
	save value: false
extra card field:
	type: multiple choice
	name: stamp
	empty choice: colorless
	choice: white
	choice: blue
	choice: black
	choice: red
	choice:
		name: green
		line below: true
	choice: artifact
	choice: land
	choice: multicolor
	choice:
		name: hybrid
		line below: true
	choice:
		name: horizontal
		type: radio
	choice:
		name: vertical
		type: radio
	choice:
		name: radial
		type: radio
	choice:
		name: diagonal
		type: radio
	choice:
		name: overlay
		type: radio
	choice:
		name: reversed
	script: card_color_filter(value)
	default: card_color(casting_cost: card.casting_cost, rules_text: card.rule_text, type: card.super_type, watermark: card.watermark, card_name: card.name)
extra card field:
	type: text
	name: auto copyright
	script: set.copyright
	save value: false
	editable: false
	show statistics: false
extra card field:
	type: choice
	name: flavor bar
	choice: bar
	editable: false
	save value: false
	show statistics: false

extra card field:
	type: choice
	name: crown
	script: card.card_color
	editable: false

extra card field:
	type: choice
	name: level 2 overlay
	script: card.card_color
	choice: mask
	editable: false
	show statistics: false
extra card field:
	type: choice
	name: level 3 overlay
	script: card.card_color
	choice: mask
	editable: false
	show statistics: false
extra card field:
	type: choice
	name: level 4 overlay
	script: card.card_color
	choice: mask
	editable: false
	show statistics: false



extra card style:
	dot:
		left: {48.5 + card_style.set_code.content_width}
		right: {card_style.language_code.left}
		top: 998
		#width: 10
		height: 20
		z index: 1
		alignment: middle center
		font:
			name: Magic Gotham Medium
			size: {13-shrink_setcode()}
			color: white

	card code:
		left: 48.5
		top: 979
		width: 20
		height: 20
		z index: 1
		font:
			name: Magic Gotham Medium
			size: 13
			color: white
	card number:
		left: 69
		top: 979
		width: 220
		height: 20
		z index: 1
		font:
			name: Magic Gotham Medium
			size: 13
			color: white

	artist arrow:
		left: { 143 }
		top: 1002
		width: {24*0.9}
		height: {15*0.9}
		z index: 1
		render style: image
		image: artist_arrow.png
	pt box:
		left:	0
		top:	0
		width:	750
		height:	1050
		z index: 1
		visible: { card.pt != "" }
		render style: image
		image: { card_ptbox() }
	stamp:
		left:	319
		top:	949
		width:	112.5
		height:	52
		z index: 5
		visible: { (is_rare() and styling.use_holofoil_stamps) }
		render style: image
		image: { card_stamp() }
		# mask: stamp_mask.png
		
	auto copyright:
		right: 699
		top: { if card.pt != "" then 998 else 981 }
		width: 252
		height: 20
		z index: 2
		alignment: middle right shrink-overflow
		visible: {set.automatic_copyright}
		font:
			name: mplantin
			size: 13
			color: white
			
	flavor bar:
		left: 0
		top: { bar_equation() -4}
		width: 750
		height: 7
		z index: 1
		render style: image
		image: bar.png
		visible: { card.rule_text != "" and remove_tags(card.flavor_text) != "" and set.use_flavor_bar }
	crown:
		left: 0
		top: 0
		width:	750
		height:	1050
		render style: image
		image: { crown_background() }
		mask: { "" }
		z index: 0

	level 2 overlay:
		left: 58
		top: { if is_d20() then top_of_para(rolls_start(card.text)) else 0}
		height: { if is_d20() then height_of_para(rolls_start(card.text)) else 0}
		width: 630
		z index: 0
		render style: image
		image: {overlay1()}
		visible: {(is_d20() and number_of_rolls(card.text) > 0)}
	level 3 overlay:
		left: 58
		top: { if is_d20() then top_of_para(rolls_start(card.text)+2) else 0}
		height: { if is_d20() then height_of_para(rolls_start(card.text)+2) else 0}
		width: 630
		z index: 0
		render style: image
		image: {overlay2()}
		visible: {(is_d20() and number_of_rolls(card.text)-no_roll(card.text) > 2)}
	level 4 overlay:
		left: 58
		top: { if is_d20() then top_of_para(rolls_start(card.text)+4) else 0}
		height: { if is_d20() then height_of_para(rolls_start(card.text)+4) else 0}
		width: 630
		z index: 0
		render style: image
		image: {overlay3()}
		visible: {(is_d20() and number_of_rolls(card.text)-no_roll(card.text) > 4) }
