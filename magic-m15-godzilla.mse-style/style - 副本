mse version: 2.0.0
game: magic
short name: M15 Godzilla
full name: M15 Godzilla Style
icon: card-sample.png
installer group: magic/m15 style/normal cards
position hint: 018

###################################### Template made by Redwavez

version: 2020-05-11
depends on:
	package: magic.mse-game
	version: 2020-04-25
depends on:
	package: magic.mse-game
	version: 2014-06-25
depends on:
	package: magic-default-image.mse-include
	version: 2007-09-23
depends on:
	package: magic-watermarks.mse-include
	version: 2007-09-23
depends on:
	package: magic-identity-new.mse-include
	version: 2012-01-22
depends on:
	package: magic-mana-large.mse-symbol-font
	version: 2007-09-23
depends on:
	package: magic-mana-small.mse-symbol-font
	version: 2007-09-23

card width: 375
card height: 523
card dpi: 150
############################################################## Extra scripts
init script:
	# Load scripts for image box
	include file: /magic-default-image.mse-include/scripts
	
	# Should hybrids have a grey name?
	mask_hybrid_with_land := { styling.grey_hybrid_name }
	
	#Should multicolor lands with basic land types have a colored name?
	mask_multi_land_with_color := { styling.colored_multicolor_land_name }

	tall_background := {color_background(type: "tall", base_hybrid: card_hybrid)}
	short_background := {color_background(type: "short", base_hybrid: card_hybrid)}

	is_tall := {chosen(styling.frame_options, choice: "tall")}
	is_short := {chosen(styling.frame_options, choice: "short") and not is_tall()}
	is_legend := {chosen(styling.frame_options, choice: "legend") or (set.auto_legends and contains(card.super_type, match:"Legendary"))}
	no_als := {chosen(styling.frame_options, choice: "no alias")}
	show_border := {chosen(styling.frame_options, choice: "show border")}
	show_bar := {not chosen(styling.frame_options, choice: "no flavor bar")}
	is_auto := {chosen(styling.crown, choice: "auto")}
	is_nyx := { chosen(styling.crown, choice: "nyx") or ( is_auto() and match(card.super_type, match: "Enchantment") and match(card.super_type, match: "Creature" ) and not is_com() ) }
	is_com := { chosen(styling.crown, choice: "companion") or ( is_auto() and match(rules_text(), match: "Companion") ) }
	is_custom := {chosen(styling.crown, choice: "custom")}

	
	chop_top := {if number_of_items(in: break_text(match: "[^,]+", styling.chop_text)) < 2 then 0 else to_number(break_text(match: "[^,]+", styling.chop_text)[0])}
	
	chop_bottom := {if number_of_items(in: break_text(match: "[^,]+", styling.chop_text)) < 2 then 0 else to_number(break_text(match: "[^,]+", styling.chop_text)[1])}


	rules_text := {remove_tags(replace(replace(card.text,match:"<i-flavor>.*</i-flavor>",replace:""), match: "[\t\n\r]", replace:""))}
	bar_visibility := {if rules_text() != "" and show_bar() and remove_tags(card.flavor_text) != "" then true else false}
	bar_offset := {num(styling.flavor_bar_offset)}

	num := {( if input != "" then (if input[0] == "-" then to_number(substring(input, begin: 1))*(-1) else to_number(input)) else 0) or else 0}
	
	template_prefix := [card: "" pt: "" crown: "legend/" stamp: "" identity: "/magic-identity-new.mse-include/" alias: "alias/" tall: "tall/" short: "short/"]
	template_suffix := [card: "card.png" pt: "pt.png" stamp: "stamp.png" crown: "crown.png" "stamp" identity: "identity.png" alias: "card.png" tall: "card.png" short: "card.png"]
	template      := { (if is_custom() and type == "crown" then ("custom_crown" + styling.custom_crown_location + "/") else ( template_prefix[type] + (if type == "crown" and no_als() then "noals/" else "") + (if is_nyx() and type == "crown" then "nyx/" else if is_com() and type == "crown" then "com/") ) ) + input + template_suffix[type]}
	land_template := template
	# Use land templates for previews because they show more contrast
	hybrid_previews := "land,hybrid"
	
	# Use the normal tap symbol
	mana_t := {
		if      styling.tap_symbol == "old"        then "old"
		else if styling.tap_symbol == "diagonal T" then "older"
		else                                            "new"
	}
	
	# Use guild mana symbols?
	guild_mana := { styling.use_guild_mana_symbols }
	
	# Is the card a promo card?
	is_promo := { styling.promo }
	mana_sort := {
		if styling.unsorted_casting_cost then
			input
		else
			sort_text@(order: "XYZI[0123456789]HSCA(WUBRG)")()
	}
	ancestral_mana := { styling.ancestral_mana_symbols }
	has_identity := { styling.color_indicator_dot }
	is_unsorted := {styling.remove_from_autocount}
	
############################################################## Set info fields
set info style:
	symbol:
		variation:
			name: invertedcommon
			border radius: 0.10
			fill type: solid
			fill color:   rgb(255,255,255)
			border color: rgb(0,0,0)
############################################################## Extra style options
styling field:
	type: multiple choice
	name: frame options
	description: Other frame options
	choice: legend
	choice: tall
	choice: short
	choice: no alias
	choice: show border
	choice: no flavor bar
styling field:
	type: text
	name: chop text
	description: Written as a,b, where a is the top chop and b is the bottom chop.
styling field:
	type: text
	name: font size
	description: Changes the rules text's font size (default is 14)
styling field:
	type: text
	name: shrink name text
	description: Shrinks the name's font size
styling field:
	type: text
	name: shrink typeline text
	description: Shrinks the typeline's font size
styling field:
	type: text
	name: flavor bar offset
	description: Moves the flavor bar n pixels (positive number for down, negative number for up).
styling field:
	type: choice
	name: crown
	description: The legend crown type. Auto recognizes it automatically (companion overrides nyx). Custom uses the "custom_crown" folder.
	choice: auto
	choice: standard
	choice: nyx
	choice: companion
	choice: custom
	initial: auto
styling field:
	type: text
	name: custom crown location
	description: Can be used to target a specific place inside the "custom_crown" folder. No need to insert a "/" in the end of the location's name.
styling field:
	type: boolean
	name: use holofoil stamps
	description: Change to no to disable rare holofoil stamps
styling field:
	type: boolean
	name: color indicator dot
	description: Use the color indicator dot
	initial: no
styling field:
	type: boolean
	name: remove from autocount
	description: Removes the automatic card number for specific cards, to allow for overcounted cards like in Planeswalker Decks.
	initial: no
styling field:
	type: boolean
	name: grey hybrid name
	description: Use a grey background for the name and type line on hybrid cards. This is done on real cards.
styling field:
	type: boolean
	name: unsorted casting cost
	description: Disables automatic mana symbol sorting.
	initial: no
styling field:
	type: boolean
	name: ancestral mana symbols
	description: Enables ancestral's modified generic mana.
	initial: no
styling field:
	type: boolean
	name: colored multicolor land name
	description: Use a colored background for the name and type on multicolor land cards with a basic land type. This is done on real cards.
styling field:
	type: boolean
	name: use guild mana symbols
	description: Use the Ravnica guild symbols instead of the official half/half circles for hybrid mana.
	initial: no
styling field:
	type: choice
	name: tap symbol
	description: What tap and untap symbols should be used on cards?
	initial: modern
	choice: modern
	choice: old
	choice: diagonal T
styling field:
	type: package choice
	name: text box mana symbols
	match: magic-mana-*.mse-symbol-font
	initial: magic-mana-small.mse-symbol-font
styling field:
	type: choice
	name: center text
	description: When to center text (short text only means only on one-line cards with no flavor text)
	choice: always
	choice: short text only
	choice: never
	initial: never
styling field:
	type: boolean
	name: promo
	description: Is this card a promo card, with the "P" rarity?
	initial: no
styling field:
	type: boolean
	name: masterpiece
	description: Is this card a Masterpiece?
	initial: no
styling field:
	type: boolean
	name: inverted common symbol
	description: Should the common rarity symbol be inverted, like in Coldsnap?
	initial: no
styling field:
	type: package choice
	name: overlay
	description: Should there be an overlay applied, such as foil?
	match: magic-overlay-*.mse-include
	required: false
styling style:
	use guild mana symbols:
		choice images:
			yes: /magic-mana-small.mse-symbol-font/mana_guild_rg.png
			no:  /magic-mana-small.mse-symbol-font/mana_rg.png
	tap symbol:
		render style: both
		choice images:
			modern:     /magic-mana-large.mse-symbol-font/mana_t.png
			old:        /magic-mana-large.mse-symbol-font/mana_t_old.png
			diagonal T: /magic-mana-large.mse-symbol-font/mana_t_older.png
	center text:
		render style: both
		choice images:
			always:          /magic.mse-game/icons/center-all-text.png
			short text only: /magic.mse-game/icons/center-short-text.png
			never:           /magic.mse-game/icons/center-no-text.png
	inverted common symbol:
		choice images:
			no:  { symbol_variation(symbol: set.symbol, variation: "common") }
			yes: { symbol_variation(symbol: set.symbol, variation: "invertedcommon") }
	frame options:
		render style: checklist
		direction: vertical

############################################################## Card fields
card style:
	alias:
		left: 54
		top: 58
		width: 268
		height: 15
		z index: 3
		visible: {if no_als() then "false" else "true"}
		font:
			name: MPlantin-Italic
			size: 10
			scale down to: 6
			color: white
		alignment: middle center
	############################# Background stuff
	border color:
		left: 0
		top : 0
		width:	375
		height:	523
		radius: 18
		left width:   17
		right width:  17
		top width:    17
		bottom width: 18
		z index: 4
		mask: {if show_border() and is_legend() and to_text(card.border_color) == "rgb(200,200,200)"  then "crown_border_silver.png" else if show_border() and is_legend() then "crown_border.png" else if show_border() then "border_mask.png" else "no_border_mask.png"}
	card color:
		left:	0
		top:	0
		width:	375
		height:	523
		z index: 0
		render style: image
		popup style: in place
		image: {if is_tall() then tall_background() else if is_short() then short_background() else card_background()}
	############################# Name line
	
	name:
		left: { if card.card_symbol=="none" then 32 else 50 }
		top: {30 - num(styling.shrink_name_text)}
		right:  { 341 - card_style.casting_cost.content_width }
		height: 23
		alignment: bottom shrink-overflow
		padding bottom: 0
		z index: 5
		font:
			name: Beleren Bold
			size: {16 - num(styling.shrink_name_text)}
			color: white
	casting cost:
		right: 346
		top: 29
		width: { max(30, card_style.casting_cost.content_width) + 5 }
		height: 23
		alignment: middle right
		font:
			name: MPlantin
			size: 15
		symbol font:
			name: magic-mana-large
			size: 15
			alignment: middle right
		always symbol: true
		z index: 5
		padding top: 0
	card symbol:
		left: {if card.card_symbol=="none" then 20 else 30}
		top: 29
		height: 20
		width: 14
		z index: 1
		render style: image
		choice images:
			tombstone: tombstone.png
	############################# Image
	image:
		left: 0
		top: 0
		width: 375
		height:	523
		z index: 1
		default: {default_image(card.card_color)}
		mask: {"mask" + (if no_als() then "" else "_alias") + (if is_tall() then "_tall" else if is_short() then "_short" else "") + ".png"}
	############################# Card type
	indicator:
		left: 31
		top: {if is_tall() then 300 else if is_short() then 370 else 344}
		width: 17
		height: 17
		z index: 2
		render style: image
		visible: { has_identity() }
		image: { card_identity() }
	type:
		left: { if has_identity() then "52" else "32" }
		top: {(if is_tall() then 296 else if is_short() then 366 else 340) + num(styling.shrink_typeline_text)}
		width: { (if has_identity() then "290" else "310") - max(22,card_style.rarity.content_width) }
		height: 20
		alignment: top shrink-overflow
		z index: 3
		padding top: 2
		font:
			name: Beleren Bold
			size: {13 - num(styling.shrink_typeline_text)}
			color: white
			separator color: red
	rarity:
		right: 344
		top: {if is_tall() then 297 else if is_short() then 367 else 341}
		width: 44
		height: 22
		z index: 2
		render style: image
		alignment: middle right
		choice images:
			# Images based on the set symbol
			basic land:
				script:
					if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
					else symbol_variation(symbol: set.symbol, variation: "common")
			common:
				script:
					if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
					else symbol_variation(symbol: set.symbol, variation: "common")
			uncommon:   script: symbol_variation(symbol: set.symbol, variation: "uncommon")
			rare:       script: symbol_variation(symbol: set.symbol, variation: "rare")
			mythic rare:       script: symbol_variation(symbol: set.symbol, variation: "mythic rare")
			special:    script: symbol_variation(symbol: set.symbol, variation: "special")
			masterpiece: script: symbol_variation(symbol: set.masterpiece_symbol, variation: "mythic rare")
	############################# Text box
	text:
		left: 29
		top: {(if is_tall() then 327 else if is_short() then 396 else 370)+chop_top()}
		width: 314
		height: {(if is_tall() then 154 else if is_short() then 89 else 115)-chop_bottom()}
		font:
			name: MPlantin
			italic name: MPlantin-Italic
			size: {if to_number(styling.font_size) > 0 and to_number(styling.font_size) < 14 then to_number(styling.font_size) else 14}
			scale down to: 6
			color: white
		symbol font:
			name: { styling.text_box_mana_symbols }
			size: 14
		alignment:
			script:
				if (styling.center_text == "short text only" and
				   not contains(match:"\n", card.rule_text) and
				   card.flavor_text == "<i-flavor></i-flavor>" and
				   card_style.text.content_lines <= 2) or
				   styling.center_text == "always"
				then "middle center"
				else "middle left"
		z index: 4
		padding left:   6
		padding right:  4
		line height hard: 1.2
		line height line: 1.5
		line height soft: 0.9
		line height hard max: 1.3
		line height line max: 1.6
	watermark:
		left: 117
		top: {if is_tall() then 321 else if is_short() then 400 else 374}
		width: 138
		height: {if is_tall() then 166 else if is_short() then 82 else 108}
		z index: 3
		render style: image
		popup style: in place
		alignment: middle center
		include file: /magic-watermarks.mse-include/watermarks
	############################# PT
	pt:
		z index: 5
		left: 286
		top: 469
		width: 60
		height:	28
		alignment: center middle shrink-overflow
		font:
			name: Beleren Bold
			size: 16
			color: white
			separator color: red
	############################# Card sorting / numbering
	set code:
		left: 24
		top: 498
		width: 40
		height: 10
		z index: 3
		font:
			name: Relay-Medium
			size: 7
			color: white
			weight: bold
	############################# Copyright stuff
	illustrator:
		left: { 44 + card_style.set_code.content_width }
		top: 497.5
		width: 200
		height: 10
		z index: 3
		font:
			name: Beleren Small Caps Bold
			size: 7.25
			color: white
	copyright:
		right: 350
		top: { if card.pt != "" then 500 else 488 }
		width: 140
		height: 10
		z index: 3
		visible: {not set.automatic_copyright}
		alignment: middle right shrink-overflow
		font:
			name: Matrix
			size: 7
			color: white
			weight: bold
############################################################## Extra card fields
extra card field:
	type: text
	name: card code
	save value: false
	script:
		if set.automatic_card_numbers and not styling.remove_from_autocount then
			forward_editor(prefix: card_number_m15() + "/" + card_count_m15() + " " + rarity_code() + " ", field: card.card_code_text)
		else
			combined_editor(field1: card.custom_card_number, separator: " " + rarity_code() + " ", field2: card.card_code_text)
extra card field:
	type: choice
	name: artist arrow
	editable: false
	save value: false
	choice: white
extra card field:
	type: choice
	name: pt box
	script: card.card_color
	editable: false
	save value: false
extra card field:
	type: choice
	name: foil stamp
	choice: stamp
	save value: false
	editable: false
extra card field:
	type: choice
	name: foil layer
	choice: foil
	save value: false
	editable: false
extra card field:
	type: choice
	name: cover
	choice: foil
	save value: false
	editable: false
extra card field:
	type: multiple choice
	name: stamp
	empty choice: colorless
	choice: white
	choice: blue
	choice: black
	choice: red
	choice:
		name: green
		line below: true
	choice: artifact
	choice: land
	choice: multicolor
	choice:
		name: hybrid
		line below: true
	choice:
		name: horizontal
		type: radio
	choice:
		name: vertical
		type: radio
	choice:
		name: radial
		type: radio
	choice:
		name: diagonal
		type: radio
	choice:
		name: overlay
		type: radio
	choice:
		name: reversed
	script: card_color_filter(value)
	default: card_color(casting_cost: card.casting_cost, rules_text: card.rule_text, type: card.super_type, watermark: card.watermark, card_name: card.name)
extra card field:
	type: text
	name: auto copyright
	script: set.copyright
	save value: false
	editable: false
	show statistics: false
extra card field:
	type: choice
	name: crown
	script: card.card_color
	editable: false
	save value: false
	show statistics: false
extra card field:
	type: choice
	name: als
	script: card.card_color
	editable: false
	save value: false
	show statistics: false
extra card field:
	type: choice
	name: flavor bar
	choice: bar
	editable: false
	save value: false
	show statistics: false
extra card style:
	card code:
		left: 24
		top: 488
		width: 120
		height: 10
		z index: 3
		font:
			name: Relay-Medium
			size: 7
			color: white
			weight: bold
	artist arrow:
		left: { 28 + card_style.set_code.content_width }
		top: 500
		width: 12
		height: 7
		z index: 3
		render style: image
		image: artist_arrow.png
	pt box:
		left: 273
		top: 466
		width: 81
		height:	42
		z index: 3
		visible: { card.pt != "" }
		render style: image
		image: { card_ptbox() }
	foil layer:
		left: 0
		top : 0
		width: 375
		height: 523
		z index: 3
		render style: image
		image: {if styling.overlay == "" then nil else styling.overlay + "/overlay.png"}
		mask: { if is_rare() then "foil_mask_rare.png" else "foil_mask.png" }
	stamp:
		left: 165
		top: 472
		width: 46
		height: 26
		z index: 2
		visible: { is_rare() and styling.use_holofoil_stamps }
		render style: image
		image: { card_stamp() }
		mask: stamp_mask.png
	foil stamp:
		left: 165
		top: 472
		width: 46
		height: 26
		z index: 3
		visible: { is_rare() and styling.use_holofoil_stamps }
		render style: image
		image: foil_stamp.png
	auto copyright:
		right: 350
		top: { if card.pt != "" then 500 else 488 }
		width: 140
		height: 10
		z index: 2
		alignment: middle right shrink-overflow
		visible: {set.automatic_copyright}
		font:
			name: Matrix
			size: 7
			color: white
			weight: bold
	crown:
		left: 0
		top: 0
		width: 375
		height: 523
		z index: 2
		render style: image
		image: { color_background(type: "crown", base_hybrid: card_hybrid)}
		visible: {is_legend()}

	als:
		left: 0
		top: 0
		width: 375
		height: 523
		render style: image
		image: {color_background(type: "alias", base_hybrid: card_hybrid)}
		z index: 2
		visible: {if no_als() then "false" else "true"}

	
	
	flavor bar:
		left: 0
		top: {bar_equation() + bar_offset()}
		width: 375
		height: 1
		z index: 5
		visible: {bar_visibility()}
		render style: image
		image: bar.png
	cover:
		left: 0
		top: 0
		width: 375
		height: 523
		z index: 1
		render style: image
		image: {"cover/mask" + (if no_als() then "" else "_alias") + (if is_tall() then "_tall" else if is_short() then "_short" else "") + ".png"}
